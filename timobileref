#!/usr/bin/perl

use strict;
use warnings;
use 5.008;
use File::Basename;
use File::Find ();
use File::ShareDir ();
use Getopt::Long;

our $VERSION = '0.01';

my $opt_docdir;
GetOptions(
    'docdir' => \$opt_docdir,
);

my $BROWSER = $ENV{BROWSER} || 'w3m';
my %BROWSER_OPTION_MAP = (
    w3m  => [qw(-T text/html)],
    lynx => ['-stdin'],
);
my $BASE_DIR = "latest";

my $doc_dir = $ENV{TIMOBILEREF_DOCROOT} || '';
if (! $doc_dir) {
    eval {
	$doc_dir = File::ShareDir::dist_dir('timobileref');
    };
}
if ($opt_docdir) {
    print "$doc_dir\n";
    exit 0;
}

my $s = shift @ARGV || "";
$s =~ s/^Ti(\.|$)/Titanium$1/ if $s;
my $name = $s || $BASE_DIR;

$name = "$BASE_DIR/$name" unless -e "$doc_dir/$name.html";
for my $suffix qw(object module method property proxy) {
    if (-e "$doc_dir/$name-$suffix.html") {
        $name .= "-$suffix";
        last;
    }
}

# try by name
open_browser("$doc_dir/$name.html")
    if -e "$doc_dir/$name.html";

my @cand;
my %cand_dir;
File::Find::find(
    {
        wanted => sub {
            my $fn = $_;
            push @cand, $fn
                if ($fn =~ m{.*/.*$s.*-.*\.html}i);
        },
        no_chdir => 1,
    },
    $doc_dir,
);
if (@cand == 0) {
    print STDERR "no document found for: $s\n";
    exit 1;
} elsif (@cand == 1) {
    open_browser($cand[0]);
} else {
    pipe my $rfh, my $wfh
        or die "failed to create pipe:$!";
    my $pid = fork;
    die "fork failed:$!"
        unless defined $pid;
    unless ($pid) {
        print $wfh <<"EOT";
<title>Search results    [Titanium Mobile Reference]</title>
<div class="breadcrumbs">
<span class="bchead">You are here: </span><a href="$doc_dir/$BASE_DIR.html"  title="start">Titanium Mobile Reference</a> &raquo; Search results
</div>
<ul>
EOT
        for my $cand (@cand) {
            my $name = $cand;
            $name =~ s{$doc_dir/}{};
            $name =~ s{(/$BASE_DIR|)\.html$}{};
            $name =~ s{/}{ &raquo; }g;
            printf $wfh qq{<li><a href="%s">%s</a></li>\n}, $cand, $name;
        }
        print $wfh <<"EOT";
</ul>
EOT
        exit 0;
    }
    open STDIN, '<&', $rfh
        or die "cannot dup STDIN:$!";
    close $rfh;
    open_browser(@{ $BROWSER_OPTION_MAP{$BROWSER} || [] });
}

sub open_browser {
    exec $BROWSER, @_;
    die "failed to exec browser ($BROWSER):$!";
}

__END__

=head1 NAME

timobileref - man-style access to titanium-mobile documents (using w3m)

=head1 SYNOPSIS

  % timobileref          # prints top page
  % timobileref Titanium # prints Titanium module docs
  % timobileref Titanium.addEventListener # prints Titanium.addEventListener method docs

=head1 INSTALL

  $ perl Makefile.PL
  $ make
  $ make install

=head1 AUTHOR

pekepeke

=head1 LICENSE

This program is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

See http://www.perl.com/perl/misc/Artistic.html

The documents are from 
    http://developer.appcelerator.com/apidoc/mobile/latest

=cut
